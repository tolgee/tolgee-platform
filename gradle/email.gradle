/*
 * Copyright (C) 2024 Tolgee s.r.o. and contributors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: "$rootDir/gradle/utils.gradle"

def emailPath = "${rootDir}/email"

tasks.register('copyEmailResources', Copy) {
    from "${emailPath}/resources"
    // Not a typo: static so Spring picks it up, then static so the url is /static/emails
    into "${project.projectDir}/build/generated/resources/static/static/emails/."
}

tasks.register('copyEmailTemplates', Copy) {
    dependsOn 'buildEmails'

    from "${emailPath}/dist/email-templates"
    into "${project.projectDir}/build/generated/resources/email-templates"
}

tasks.register('copyEmailLocaleData', Copy) {
    dependsOn 'updateEmailTranslations'

    from "${emailPath}/i18n"
    into "${project.projectDir}/build/generated/resources/email-i18n"
}

tasks.register('installEmailDeps', Exec) {
    onlyIf { System.getenv("SKIP_EMAIL_BUILD") != "true" }

    workingDir = emailPath
    commandLine npmCommandName, "ci"

    inputs.file("${emailPath}/package.json")
    inputs.file("${emailPath}/package-lock.json")
    outputs.dir("${emailPath}/node_modules")
}

tasks.register('buildEmails', Exec) {
    dependsOn 'installEmailDeps'
    onlyIf { System.getenv("SKIP_EMAIL_BUILD") != "true" }

    workingDir = emailPath
    commandLine npmCommandName, "run", "export"

    inputs.dir("${emailPath}/emails")
    inputs.dir("${emailPath}/components")
    inputs.file("${emailPath}/tailwind.config.ts")
    inputs.file("${emailPath}/package.json")
    inputs.file("${emailPath}/package-lock.json")
    outputs.dir("${emailPath}/dist")
}

tasks.register('updateEmailTranslations', Exec) {
    onlyIf {
        System.getenv("SKIP_EMAIL_BUILD") != "true" &&
                System.getenv("TOLGEE_API_URL") != null &&
                System.getenv("TOLGEE_API_URL") != ""
    }

    workingDir = emailPath
    commandLine npmCommandName, "run", "tg:pull"
}
