apply from: './gradle/utils.gradle'

import groovy.io.FileType

import java.time.Duration

ext {
    E2E_DIR = "${project.projectDir}/e2e"
    BILLING_E2E_DIR = "${project.projectDir}/../billing/e2e"
    WEBAPP_DIR = "${project.projectDir}/webapp"
}
def dockerPath = ["which", "docker"].execute().text.trim()

def getE2eChunks() {
    def totalJobs = System.getenv("E2E_TOTAL_JOBS")
    def list = []

    def dir = new File("$E2E_DIR/cypress/e2e")
    dir.eachFileRecurse(FileType.FILES) { file ->
        if (file.path.matches("^.*\\.tsx?${'$'}")) {
            list.add(file.path.replace("$E2E_DIR/", ""))
        }
    }

    list.sort()

    if (totalJobs == null) {
        return List.of(list)
    }

    def remainingChunks = Integer.parseInt(totalJobs)
    def chunks = [] as ArrayList<List<String>>
    while (remainingChunks > 0) {
        def toIndex = Math.ceil(list.size().toFloat() / remainingChunks.toFloat()).toInteger()
        chunks.add(list.subList(0, toIndex))
        list = list.subList(toIndex, list.size())
        remainingChunks--
    }
    return chunks
}

def getE2eTestsToRun() {
    def chunks = getE2eChunks()

    def jobIndex = System.getenv("E2E_JOB_INDEX")

    if (jobIndex == null) {
        return chunks.flatten()
    }

    return chunks[Integer.parseInt(jobIndex)]
}

tasks.register('waitForRunningContainer', Task) {
    group = 'e2e'
    doLast {
        while (true) {
            def pb = new ProcessBuilder("docker", "compose", "logs", "-f")
            pb.directory(new File(E2E_DIR))
            def proc = pb.start()
            BufferedReader stdInput = new BufferedReader(new InputStreamReader(proc.getInputStream()))
            String s = null
            while ((s = stdInput.readLine()) != null) {
                println(s)
                if (s.contains("Tomcat started on port")) {
                    return
                }
            }
            if (proc.exitValue() != 0) {
                throw new RuntimeException("docker compose logs returned non zero exit code")
            }
        }
    }
    timeout = Duration.ofSeconds(120)
}

tasks.register('installE2eDeps', Exec) {
    group = 'e2e'
    onlyIf { System.getenv("SKIP_INSTALL_E2E_DEPS") != "true" }
    inputs.file("$E2E_DIR/package.json")
    inputs.file("$E2E_DIR/package-lock.json")
    outputs.dir("$E2E_DIR/node_modules")

    commandLine npmCommandName, "install"
    workingDir E2E_DIR
}

tasks.register('runE2e', Exec) {
    group = 'e2e'
    dependsOn "runDockerE2e", "installE2eDeps"
    def specs = getE2eTestsToRun().join(",")

    environment     "CYPRESS_HOST", "http://localhost:8201"
    commandLine npmCommandName, "run", "cy:run", "--", "--spec", specs

    workingDir E2E_DIR

    finalizedBy "saveServerLogs", "stopDockerE2e", "cleanupDockerE2e"
}

tasks.register('printE2eGroups') {
    doLast {
        getE2eChunks().indexed().forEach { index, chunk ->
            println("Chunk: $index")
            chunk.forEach { println(" $it") }
        }
    }
}

tasks.register('saveServerLogs', Exec) {
    group = 'e2e'
    commandLine "bash", "-c", "docker compose logs > server.log"
    workingDir E2E_DIR
}

tasks.register('openE2e', Exec) {
    group = 'e2e'
    dependsOn "runDockerE2e", "installE2eDeps"

    commandLine npmCommandName, "run", "cy:open"

    workingDir E2E_DIR

    finalizedBy "stopDockerE2e", "cleanupDockerE2e"
}

tasks.register('openE2eDev', Exec) {
    group = 'e2e'
    commandLine npmCommandName, "run", "cy:open"

    environment "CYPRESS_HOST", "http://localhost:8081"

    workingDir E2E_DIR
}

tasks.register('runDockerE2e', Exec) {
    group = 'e2e'
    dependsOn "tagDockerLocal"
    commandLine dockerPath, "compose", "up", "-d"
    workingDir E2E_DIR
    finalizedBy "waitForRunningContainer"
}

tasks.register('runDockerE2eDev', Exec) {
    group = 'e2e'
    commandLine dockerPath, "compose", "up", "-d", "fakesmtp"
    workingDir E2E_DIR
}

tasks.register('stopDockerE2e', Exec) {
    group = 'e2e'
    commandLine dockerPath, "compose", "stop"
    workingDir E2E_DIR
}

tasks.register('cleanupDockerE2e', Exec) {
    group = 'e2e'
    commandLine dockerPath, "compose", "rm", "-f"
    workingDir E2E_DIR
}

tasks.register('tagDockerLocal', Exec) {
    group = 'e2e'
    dependsOn "docker"
    commandLine dockerPath, "tag", "tolgee/tolgee", "tolgee/tolgee:local"
}
